// Gradle build is actively used for Travis CI build (from GitHub)
// Maven is still used for release though
plugins {
	id 'idea'
	id 'maven'
// See notes in .travis.yml
//	id 'jacoco'
//	id 'com.github.kt3k.coveralls' version '2.6.3'
}

//jacocoTestReport {
//	reports {
//		xml.enabled = true // coveralls plugin depends on xml format report
//		html.enabled = true
//	}
//}

group = 'org.javasimon'

repositories {
	mavenCentral()
}

ext.springVersion = '3.2.0.RELEASE'
ext.slf4jVersion = '1.7.2'
ext.javaEeVersion = '6.0'
ext.mockitoVersion = '1.9.0'
ext.h2Version = '1.4.190'
ext.testNgVersion = '6.5.2'
ext.gsonVersion = '2.2.4'

ext.libs = [
	spring_core: "org.springframework:spring-core:$springVersion",
	spring_aop : "org.springframework:spring-aop:$springVersion",
	spring_jdbc: "org.springframework:spring-jdbc:$springVersion",
	spring_mvc : "org.springframework:spring-webmvc:$springVersion",
	spring_web : "org.springframework:spring-web:$springVersion",
	spring_test: "org.springframework:spring-test:$springVersion",
	slf4j      : "org.slf4j:slf4j-api:$slf4jVersion",

	javaee_api : "javax:javaee-api:$javaEeVersion",
	mockito    : "org.mockito:mockito-core:$mockitoVersion",
	h2         : "com.h2database:h2:$h2Version",
	test_ng    : "org.testng:testng:$testNgVersion",
	gson       : "com.google.code.gson:gson:$gsonVersion"
]

subprojects {
	apply plugin: 'java'
	apply plugin: 'jacoco'

	repositories {
		mavenCentral()
	}

	dependencies {
		testCompile libs.test_ng
		testCompile libs.mockito
		testCompile libs.spring_test
	}

	version = '4.1.4'

	sourceCompatibility = '1.7'
	targetCompatibility = '1.7'

	jar {
		manifest.attributes provider: 'gradle'
	}

	test {
		useTestNG()
	}
}
